         PRINT ON
* SORRY...NO WARRANTY BUT QUESTIONS OR INQUIRIES CAN BE
* DIRECTED TO: SEBASTIANÂ§WELTON.DE
*              WWW.WELTON.DE
QWKX     CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)                            STANDARD H/K
         LR    R12,R15                            STANDARD H/K
         USING QWKX,R12                           STANDARD H/K
         ST    13,SAVE+4                          STANDARD H/K
         LA    R11,SAVE                           STANDARD H/K
         ST    R11,8(R13)                         STANDARD H/K
         LR    R13,R11                            STANDARD H/K
         EJECT
*
* ------------------------------ *
* INSTRUCTIONS AFTER THIS POINT  *
* ------------------------------ *
*
         L     R10,0(R1)                          LOAD PARM ADDRESS
         MVC   FLD#1(08),0(R10)                   SHIFT TARGET FIELD
         MVC   FLD#2(08),8(R10)                   SHIFT MASK FIELD
         LH    R3,=H'7'                           LOAD MAX OFFSET
QWKX010  LA    R2,FLD#1                           ADDRESS BIG FIELD
         AR    R2,R3                              ADD TO ADDRESS
         CLI   0(R2),C' '                         CHECK FOR SPACE
         BNE   QWKX015                            AHA, NO SPACE
         BCT   R3,QWKX010                         DECREMENT/LOOP
         LA    R15,4                              NO DATA, SET RC
         B     END#                               FINISH
QWKX015  AH    R3,=H'1'                           INCREMENT LENGTH
         STH   R3,LEN#1                           STORE RESULT
         LH    R3,=H'7'                           LOAD MAX OFFSET
QWKX020  LA    R2,FLD#2                           ADDRESS LITTLE FIELD
         AR    R2,R3                              ADD TO ADDRESS
         CLI   0(R2),C' '                         CHECK FOR SPACE
         BNE   QWKX025                            AHA, GOT IT
         BCT   R3,QWKX020                         DECREMENT/LOOP
QWKX025  LH    R4,LEN#1                           LOAD TARGET LENGTH
         AH    R3,=H'1'                           INCREMENT MASK LEN
         CR    R3,R4                              COMPARE LENGTHS
         BNH   QWKX030                            MASK NOT HIGH, OK
         LA    R15,4                              HIGH, SET RC
         B     END#                               AND END
QWKX030  LR    R5,R4                              COPY REGISTER 4
         SR    R5,R3                              AND SUBTRACT R3
         SH    R3,=H'1'                           DECREMENT R3 FOR EX
         LH    R4,=H'0'                           INITIALISE INDEXER
QWKX035  LA    R2,FLD#1                           ADDRESS TARGET DATA
         AR    R2,R4                              ADD INDEX REG
         EX    R3,DATACLC                         EXECUTE CLC
         BE    QWKX040                            DATA MATCH FOUND
         AH    R4,=H'1'                           INCREMENT INDEXER
         CR    R4,R5                              COMPARE TO MAXIMUM
         BNH   QWKX035                            NOT HIGH, LOOP ROUND
         LA    R15,4                              SET RC (FAIL)
         B     END#                               AND END
QWKX040  LA    R15,0                              SET RC (SUCCESS)
*
END#     L     R13,SAVE+4                         RESTORE CALLERS AREA
         RETURN (14,12),RC=(15)                   AND RETURN TO CALLER
         LTORG
SAVE     DS    9D                                 CALLERS AREA
*
DATACLC  CLC   0(0,R2),FLD#2                      USED VIA EX
*
         DS    0H
LEN#1    DS    XL2
LEN#2    DS    XL2
FLD#1    DS    CL8
FLD#2    DS    CL8
         END
