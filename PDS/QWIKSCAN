         PRINT ON
* SORRY...NO WARRANTY BUT QUESTIONS OR INQUIRIES CAN BE
* DIRECTED TO: SEBASTIANÂ§WELTON.DE
*              WWW.WELTON.DE
QWIKSCAN CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SAVE  (14,12)                            STANDARD H/K
         LR    R12,R15                            STANDARD H/K
         LA    R8,4095
         AR    R8,R12
         LA    R8,1(R8)
         USING QWIKSCAN,R12,R8                    STANDARD H/K
         ST    13,SAVE+4                          STANDARD H/K
         LA    R11,SAVE                           STANDARD H/K
         ST    R11,8(R13)                         STANDARD H/K
         LR    R13,R11                            STANDARD H/K
         EJECT
* ------------------------------ *
* INSTRUCTIONS AFTER THIS POINT  *
* ------------------------------ *
*
*
MAIN0000 EQU   *
         BAL   R9,SUB00000                        ISPF/PROG DEFINES
*
MAIN0100 EQU   *                                  DISPLAY PANEL #1
         BAL   R9,SUB01000
*
         B     END#
*
SUB00000 EQU   *
         CALL  ISPLINK,(VDEFINE,I_PV1,P_PV1,CHAR,L44),VL
         CALL  ISPLINK,(VDEFINE,I_PV2,P_PV2,CHAR,L78),VL
         CALL  ISPLINK,(VDEFINE,I_PV3,P_PV3,CHAR,L78),VL
         CALL  ISPLINK,(VDEFINE,I_PV4,P_PV4,CHAR,L78),VL
         CALL  ISPLINK,(VDEFINE,I_PV5,P_PV5,CHAR,L04),VL
         CALL  ISPLINK,(VDEFINE,I_PV6,P_PV6,CHAR,L04),VL
         CALL  ISPLINK,(VDEFINE,I_PV7,P_PV7,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_PV8,P_PV8,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_PV9,P_PV9,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_PV10,P_PV10,CHAR,L05),VL
         CALL  ISPLINK,(VDEFINE,I_PV11,P_PV11,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_ZLCD,P_ZLCD,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_ZLCM,P_ZLCM,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_ZLCU,P_ZLCU,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_ZLCN,P_ZLCN,CHAR,L05),VL
         CALL  ISPLINK,(VDEFINE,I_ACC,P_ACC,CHAR,L05),VL
         CALL  ISPLINK,(VDEFINE,I_MTH,P_MTH,CHAR,L03),VL
         CALL  ISPLINK,(VDEFINE,I_DATA,P_DATA,CHAR,L256),VL
         CALL  ISPLINK,(VDEFINE,I_LEN,P_LEN,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_MSG,P_MSG,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_MEM,P_MEM,CHAR,L08),VL
         CALL  ISPLINK,(VDEFINE,I_DATAID,P_DATAID,CHAR,L08),VL
         CALL  ISPLINK,(VGET,VGETLST),VL
         CALL  ISPLINK,(CONTROL,ERRORS,RET),VL
         BR    R9
*
SUB01000 EQU   *
         CLI   TIND,C'Y'
         BE    SUB01065
         MVC   PANEL(08),=CL8'QWIKSCN1'
         CALL  ISPLINK,(DISPLAY,PANEL),VL
         LTR   R15,R15
         BZ    SUB01005
         BR    R9
SUB01005 EQU   *
         MVI   QWKF_O,C'F'
         PACK  XACC(03),P_PV5(04)
         PACK  PMAX(03),P_PV6(04)
         ZAP   SACC(03),=P'0'
         ZAP   PACC(02),=P'0'
         ZAP   TACC(03),=P'0'
         MVI   TIND,C'N'
         CALL  ISPLINK,(TBEND,QWIKTBL),VL
         CALL  ISPLINK,(TBCREATE,QWIKTBL,QWIKTBLK,QWIKTBLN,NOWRITE),VL
         CALL  ISPLINK,(LMINIT,DATAID,PROJ,GR1,GR2,GR3,GR4,TP,P_PV1),VL
         LTR   R15,R15
         BZ    SUB01010
         MVC   MSGID(08),=CL8'QWIKS006'
         MVC   P_MSG(08),=CL8'QWIKS006'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         B     SUB01000
SUB01010 EQU   *
         CALL  ISPLINK,(LMOPEN,P_DATAID),VL
         LTR   R15,R15
         BZ    SUB01015
         MVC   MSGID(08),=CL8'QWIKS007'
         MVC   P_MSG(08),=CL8'QWIKS007'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         B     SUB01000
SUB01015 EQU   *
         MVC   P_MEM(08),=CL8' '
         CALL  ISPLINK,(LMMLIST,P_DATAID,FREE),VL
SUB01020 EQU   *
         BAL   R10,SUB01025
         CALL  ISPLINK,(LMMLIST,P_DATAID,FREE),VL
         CALL  ISPLINK,(LMCLOSE,P_DATAID),VL
         CALL  ISPLINK,(LMFREE,P_DATAID),VL
         B     SUB01000
SUB01025 EQU   *
         CALL  ISPLINK,(LMMLIST,P_DATAID,LIST,MEM,STATS),VL
         LTR   R15,R15
         BZ    SUB01030
         C     R15,=F'12'
         BE    SUB01027
         CLI   TIND,C'Y'
         BNE   SUB01026
         BR    R10
SUB01026 EQU   *
         MVC   MSGID(08),=CL8'QWIKS012'
         MVC   P_MSG(08),=CL8'QWIKS012'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         BR    R10
SUB01027 EQU   *
         MVC   MSGID(08),=CL8'QWIKS016'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         BR    R10
SUB01030 EQU   *
         ZAP   TMAX(03),=P'0'
         AP    SACC(03),=P'1'
         CP    SACC(03),=P'50'
         BL    SUB01031
         AP    TACC(03),SACC(03)
         UNPK  P_ACC(05),TACC(03)
         OI    P_ACC+04,X'F0'
         UNPK  P_MTH(03),PACC(02)
         OI    P_MTH+02,X'F0'
         MVC   MSGID(08),=CL8'QWIKS013'
         MVC   P_MSG(08),=CL8'QWIKS013'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         CALL  ISPLINK,(CONTROL,DISPLAY,LOCK),VL
         CALL  ISPLINK,(DISPLAY,PANEL),VL
         ZAP   SACC(03),=P'0'
SUB01031 EQU   *
         ZAP   ZACC(03),=P'0'
         AP    ZACC(03),TACC(03)
         AP    ZACC(03),SACC(03)
         CP    ZACC(03),XACC(03)
         BL    SUB01032
         AP    TACC(03),SACC(03)
         UNPK  P_ACC(05),TACC(03)
         OI    P_ACC+04,X'F0'
         UNPK  P_MTH(03),PACC(02)
         OI    P_MTH+02,X'F0'
         MVC   MSGID(08),=CL8'QWIKS015'
         MVC   P_MSG(08),=CL8'QWIKS015'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         BR    R10
SUB01032 EQU   *
         MVC   DATEP+00(02),P_ZLCD+06
         MVC   DATEP+03(02),P_ZLCD+03
         MVC   DATEP+06(02),P_ZLCD+00
         CLC   DATEP(02),=C'  '
         BNE   SUB01033
         MVC   DATEP(08),=C'**/**/**'
SUB01033 EQU   *
         MVC   P_ZLCD(08),DATEP
         MVC   DATEP+00(02),P_ZLCM+06
         MVC   DATEP+03(02),P_ZLCM+03
         MVC   DATEP+06(02),P_ZLCM+00
         CLC   DATEP(02),=C'  '
         BNE   SUB01034
         MVC   DATEP(08),=C'**/**/**'
SUB01034 EQU   *
         MVC   P_ZLCM(08),DATEP
SUB01035 EQU   *
         CLI   P_PV7,C' '
         BE    SUB01040
         MVC   QWKX_T(08),P_ZLCU
         MVC   QWKX_M(08),P_PV7
         CALL  QWKX,(QWKX_P),VL
         LTR   R15,R15
         BNZ   SUB01025
SUB01040 EQU   *
         CLI   P_PV8,C' '
         BE    SUB01045
         MVC   QWKX_T(08),P_ZLCD
         MVC   QWKX_M(08),P_PV8
         CALL  QWKX,(QWKX_P),VL
         LTR   R15,R15
         BNZ   SUB01025
SUB01045 EQU   *
         CLI   P_PV9,C' '
         BE    SUB01050
         MVC   QWKX_T(08),P_ZLCM
         MVC   QWKX_M(08),P_PV9
         CALL  QWKX,(QWKX_P),VL
         LTR   R15,R15
         BNZ   SUB01025
SUB01050 EQU   *
         CLI   P_PV11,C' '
         BE    SUB01055
         MVC   QWKX_T(08),P_MEM
         MVC   QWKX_M(08),P_PV11
         CALL  QWKX,(QWKX_P),VL
         LTR   R15,R15
         BNZ   SUB01025
SUB01055 EQU   *
         CALL  ISPLINK,(LMMFIND,P_DATAID,P_MEM),VL
SUB01060 EQU   *
         CALL  ISPLINK,(LMGET,P_DATAID,INVAR,DATALOC,LENVAR,L256),VL
         LTR   R15,R15
         BNZ   SUB01025
         AP    TMAX(03),=P'1'
         CP    TMAX(03),PMAX(03)
         BH    SUB01025
         MVC   QWKF_T(256),P_DATA
         MVC   QWKF_I(078),P_PV2
         MVC   QWKF_A(078),P_PV3
         MVC   QWKF_N(078),P_PV4
         MVC   QWKF_L(008),P_LEN
         CALL  QWKF,(QWKF_O),VL
         LTR   R15,R15
         BNZ   SUB01060
         MVI   TIND,C'Y'
         CALL  ISPLINK,(TBADD,QWIKTBL),VL
         AP    PACC(02),=P'1'
         B     SUB01025
SUB01065 EQU   *
         MVI   TIND,C'N'
         AP    TACC(03),SACC(03)
         UNPK  P_ACC(05),TACC(03)
         OI    P_ACC+04,X'F0'
         UNPK  P_MTH(03),PACC(02)
         OI    P_MTH+02,X'F0'
         MVC   MSGID(08),=CL8'QWIKS015'
         MVC   P_MSG(08),=CL8'QWIKS015'
         CALL  ISPLINK,(SETMSG,MSGID),VL
         CALL  ISPLINK,(TBTOP,QWIKTBL),VL
SUB01070 EQU   *
         MVC   P_MEM(10),=CL10' '
         MVC   PANEL(08),=CL8'QWIKSCN2'
         CALL  ISPLINK,(TBDISPL,QWIKTBL,PANEL),VL
         ST    R15,TBRC
         C     R15,=F'8'
         BE    SUB01000
         LH    R3,=H'43'
SUB01075 EQU   *
         LA    R2,P_PV1
         AR    R2,R3
         CLI   0(R2),X'7D'
         BE    SUB01080
         BCT   R3,SUB01075
         ABEND 99,REASON=4
SUB01080 EQU   *
         SH    R3,=H'1'
         MVC   DSN(56),=CL56' '
         LA    R2,DSN
         EX    R3,DATAMVC
         AH    R3,=H'1'
         AR    R2,R3
         MVI   0(R2),C'('
         LH    R4,=H'7'
SUB01085 EQU   *
         LA    R5,P_MEM
         AR    R5,R4
         CLI   0(R5),C' '
         BNE   SUB01090
         BCT   R4,SUB01085
         ABEND 99,REASON=8
SUB01090 EQU   *
         MVC   1(2,R5),=X'5D7D'
         MVC   1(10,R2),P_MEM
         MVC   PANEL(08),=CL8'QWIKSCN3'
         CALL  ISPLINK,(CONTROL,DISPLAY,SVE),VL
         CALL  ISPLINK,(BROWSE,DSN,VOL,PWD,PANEL),VL
         CALL  ISPLINK,(CONTROL,DISPLAY,RST),VL
         L     R15,TBRC
         C     R15,=F'0'
         BE    SUB01070
         MVC   P_MEM(10),=CL10' '
         CALL  ISPLINK,(TBDISPL,QWIKTBL),VL
         ST    R15,TBRC
         B     SUB01075
*
END#     L     R13,SAVE+4                         RESTORE CALLERS AREA
         RETURN (14,12),RC=(15)                   AND RETURN TO CALLER
         LTORG
SAVE     DS    9D                                 CALLERS AREA
TBRC     DS    F                                  CALLERS AREA
L44      DC    F'44'
L78      DC    F'78'
L05      DC    F'5'
L04      DC    F'4'
L03      DC    F'3'
L08      DC    F'8'
L256     DC    F'256'
*
DATAMVC  MVC   0(0,R2),P_PV1
DSN      DS    CL56
         DS    0H
LMGET    DC    CL8'LMGET'
LMMFIND  DC    CL8'LMMFIND'
BROWSE   DC    CL8'BROWSE'
CONTROL  DC    CL8'CONTROL'
ERRORS   DC    CL8'ERRORS'
RET      DC    CL8'RETURN'
LOCK     DC    CL8'LOCK'
VOL      DC    CL8' '
PWD      DC    CL8' '
RST      DC    CL8'RESTORE'
SVE      DC    CL8'SAVE'
INVAR    DC    CL8'INVAR'
DATALOC  DC    CL8'DATA'
LENVAR   DC    CL8'LEN'
DISPLAY  DC    CL8'DISPLAY'
PANEL    DS    CL8
VDEFINE  DC    CL8'VDEFINE'
TBEND    DC    CL8'TBEND'
TBCREATE DC    CL8'TBCREATE'
SETMSG   DC    CL8'SETMSG'
MSGID    DC    CL8'QWIKS008'
NOWRITE  DC    CL8'NOWRITE'
TBDISPL  DC    CL8'TBDISPL'
TBTOP    DC    CL8'TBTOP'
LMINIT   DC    CL8'LMINIT'
LMMLIST  DC    CL8'LMMLIST'
TBADD    DC    CL8'TBADD'
FREE     DC    CL8'FREE'
LIST     DC    CL8'LIST'
MEM      DC    CL8'MEM'
STATS    DC    CL8'YES'
LMOPEN   DC    CL8'LMOPEN'
INPUT    DC    CL8'INPUT'
LMCLOSE  DC    CL8'LMCLOSE'
LMFREE   DC    CL8'LMFREE'
VGET     DC    CL8'VGET'
VGETLST  DC    CL42'(SV1 SV2 SV3 SV4 SV5 SV6 SV7 SV8 SV9 SV11)'
QWIKTBL  DC    CL8'QWIKTBL'
QWIKTBLK DC    CL3'( )'
QWIKTBLN DC    CL36'(MEM ZLUSER ZLCDATE ZLCNORC ZLMDATE)'
DATAID   DC    CL8'XYZ'
I_DATAID DC    CL5'(XYZ)'
P_DATAID DC    CL8' '
PROJ     DC    CL8' '
GR1      DC    CL8' '
GR2      DC    CL8' '
GR3      DC    CL8' '
GR4      DC    CL8' '
TP       DC    CL8' '
TIND     DS    CL1
QWKX_P   DS    0CL16
QWKX_T   DS    CL8
QWKX_M   DS    CL8
I_PV1    DC    CL5'(PV1)'
I_PV2    DC    CL5'(PV2)'
I_PV3    DC    CL5'(PV3)'
I_PV4    DC    CL5'(PV4)'
I_PV5    DC    CL5'(PV5)'
I_PV6    DC    CL5'(PV6)'
I_PV7    DC    CL5'(PV7)'
I_PV8    DC    CL5'(PV8)'
I_PV9    DC    CL5'(PV9)'
I_PV10   DC    CL6'(PV10)'
I_PV11   DC    CL6'(PV11)'
I_MEM    DC    CL5'(MEM)'
I_MTH    DC    CL5'(MTH)'
I_ACC    DC    CL5'(ACC)'
I_ZLCD   DC    CL9'(ZLCDATE)'
I_ZLCM   DC    CL9'(ZLMDATE)'
I_ZLCN   DC    CL9'(ZLCNORC)'
I_ZLCU   DC    CL8'(ZLUSER)'
I_DATA   DC    CL6'(DATA)'
I_LEN    DC    CL5'(LEN)'
I_MSG    DC    CL5'(MSG)'
DATEP    DS    0CL8
         DS    CL2
         DC    CL1'/'
         DS    CL2
         DC    CL1'/'
         DS    CL2
P_PV1    DC    CL44' '
P_PV2    DC    CL78' '
P_PV3    DC    CL78' '
P_PV4    DC    CL78' '
P_PV5    DC    CL04' '
P_PV6    DC    CL04' '
P_PV7    DC    CL08' '
P_PV8    DC    CL08' '
P_PV9    DC    CL08' '
P_PV10   DC    CL05' '
P_PV11   DC    CL08' '
P_MEM    DC    CL10' '
P_MTH    DS    CL3' '
P_ACC    DS    CL5
SACC     DS    CL3
TACC     DS    CL3
PMAX     DS    CL3
TMAX     DS    CL3
XACC     DS    CL3
ZACC     DS    CL3
PACC     DS    CL2
P_ZLCD   DC    CL08' '
P_ZLCM   DC    CL08' '
P_ZLCU   DC    CL08' '
P_ZLCN   DC    CL05' '
P_DATA   DC    CL256' '
P_LEN    DC    CL08' '
P_MSG    DC    CL08'QWIKS008'
CHAR     DC    CL8'CHAR'
QWKF_P   DS    0CL499
QWKF_O   DS    CL1
QWKF_T   DS    CL256
QWKF_I   DS    CL78
QWKF_A   DS    CL78
QWKF_N   DS    CL78
QWKF_L   DS    CL8
NUMCHK   DS    CL5
*
         END
